#compdef op

local -a generic_subcmds=(
	'(-h --help)'{-h,--help}'[Show usage information for command.]'
)

_op() {
	integer ret=1
	local -A opt_args
	local -a args
	local -a cmds
	args=(
		'-' 'version'
		'(-)'{-v,--version}'[Show version information.]'
		'-' 'others'
		'(-h --help)'{-h,--help}'[Show usage information.]'
		':commands:->cmds'
		'::subcommands:->subcmds'
	)
	cmds=(
		'add:Grant a user access to a vault or group.'
		'create:Create an object.'
		'delete:Remove an object.'
		'edit:Edit objects and events.'
		'encode:Encode the JSON needed to create an item.'
		'get:Get details about an object.'
		'list:List objects and events.'
		'reactivate:Reactivate a suspended user.'
		'remove:Revoke user access to a vault or group.'
		'signin:Sign in to your 1Password account.'
		'signout:Sign out of your 1Password account.'
		'suspend:Suspend a user.'
		'update:Check for updates.'
	)

	_arguments -C $args[@] && ret=0

	case "$state" in
		(cmds)
			_describe -t commands 'Commands' cmds && ret=0
			;;
		(subcmds)
			case ${words[2]} in
				(create)
					_op_create
					;;
				(delete)
					_op_delete
					;;
				(edit)
					_op_edit
					;;
				(get)
					_op_get
					;;
				(list)
					_op_list
					;;
				(*)
					_op_generic_command_args
					;;
			esac
			;;
	esac

	return ret;
}

_op_create() {
	local -a subcmds=($generic_subcmds[@])
	subcmds+=(
		'document:Create a document.'
		'group:Create a group.'
		'item:Create a new item.'
		'vault:Create a new vault.'
	)
	_describe -t commands 'Subcommands' subcmds && return
}

_op_delete() {
	local -a subcmds=($generic_subcmds[@])
	subcmds+=(
		'group:Remove a group.'
		'item:Move an item to the Trash.'
		'trash:Empties the trash for a given vault.'
		'user:Completely remove a user.'
		'vault:Remove a vault.'
	)
	_describe -t commands 'Subcommands' subcmds && return
}

_op_edit() {
	local -a subcmds=($generic_subcmds[@])
	subcmds+=(
		'group:Edit a group in your 1Password account.'
		'user:Edit the name or travel mode state of a user'
	)
	_describe -t commands 'Subcommands' subcmds && return
}

_op_get() {
	local -a subcmds=($generic_subcmds[@])
	subcmds+=(
		'account:Get details about your account.'
		'document:Download a document.'
		'group:Get details about a group.'
		'item:Get item details.'
		'template:Get an item template.'
		'totp:Get the one-time password for an item.'
		'user:Get details about a user.'
		'vault:Get details about a vault.'
	)
	_describe -t commands 'Subcommands' subcmds && return
}

_op_list() {
	local -a subcmds=($generic_subcmds[@])
	subcmds+=(
		'documents:Get a list of documents.'
		'events:Get a list of events from the Activity Log.'
		'groups:Get the list of groups.'
		'items:Get a list of items.'
		'templates:Get the list of templates.'
		'users:Get the list of users.'
		'vaults:Get the list of vaults.'
	)
	_describe -t commands 'Subcommands' subcmds && return
}

_op_generic_command_args() {
	_arguments $generic_subcmds[@] && return
}

_op

